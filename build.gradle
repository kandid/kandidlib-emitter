/*
 * (C) Copyright 2005-2014, by Dominikus Diesch.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
version = '0.4.1'
group = 'de.kandid'
def theName = 'kandidlib-emitter'
def theDescription = 'Type safe model/listener pattern through class generation'

repositories {
	mavenLocal()
	mavenCentral()
}

sourceSets {
	main {java {srcDir 'src'} resources {srcDir 'src'}}
	test {java {srcDir 'test'}}
}

dependencies {
	compile group: 'org.ow2.asm', name: 'asm', version: '4.2'
	testCompile group: 'junit', name: 'junit', version: '4.11'
	testCompile files(jar.archivePath)
}

jar {baseName = theName}

task sourcesJar(type: Jar, dependsOn: classes) {
	baseName = theName
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	baseName = theName
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

if (hasProperty('signing.keyId'))
	signing {sign configurations.archives}

if (hasProperty('sonatypeUsername'))
	uploadArchives {	repositories { mavenDeployer {
		beforeDeployment {deployment -> signing.signPom(deployment)}
		repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			authentication(userName: sonatypeUsername, password: sonatypePassword)
		}
		pom.project {
			name theName
			packaging 'jar'
			description theDescription
			url 'https://github.com/kandid/' + theName
			scm {
				def gitUrl = 'scm:git@github.com:kandid/' + theName
				url gitUrl; connection gitUrl; developerConnection gitUrl;
			}
			licenses { license {
					 name 'The Apache Software License, Version 2.0'
					 url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					 distribution 'repo'
			}}
			developers {
				developer {id 'kandid'; name 'Dominikus Diesch';}
			}
		}
	}}}


artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}